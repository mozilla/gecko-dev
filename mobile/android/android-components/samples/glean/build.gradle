/* This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at http://mozilla.org/MPL/2.0/. */

buildscript {
    repositories {
        gradle.mozconfig.substs.GRADLE_MAVEN_REPOSITORIES.each { repository ->
            maven {
                url = repository
                if (gradle.mozconfig.substs.ALLOW_INSECURE_GRADLE_REPOSITORIES) {
                    allowInsecureProtocol = true
                }
            }
        }

        dependencies {
            classpath "org.mozilla.telemetry:glean-gradle-plugin:${libs.versions.mozilla.glean.get()}"
        }
    }
}

plugins {
    id "com.jetbrains.python.envs" version "$python_envs_plugin"
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    defaultConfig {
        applicationId "org.mozilla.samples.glean"
        minSdkVersion = config.minSdkVersion
        compileSdk = config.compileSdkVersion
        targetSdkVersion = config.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled = false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
        }
    }

    buildFeatures {
        viewBinding = true
        buildConfig = true
    }

    namespace = 'org.mozilla.samples.glean'
}

dependencies {
    implementation libs.mozilla.glean
    implementation project(':service-glean')
    implementation project(':service-nimbus')
    implementation project(':support-base')
    implementation project(':support-rusthttp')
    implementation project(':support-rustlog')
    implementation project(':lib-fetch-httpurlconnection')

    implementation libs.kotlin.coroutines

    implementation libs.androidx.appcompat
    implementation libs.androidx.browser

    implementation project(':samples-glean-library')

    androidTestImplementation libs.androidx.test.core
    androidTestImplementation libs.androidx.test.monitor
    androidTestImplementation libs.androidx.test.runner
    androidTestImplementation libs.androidx.test.rules
    androidTestImplementation libs.androidx.test.junit
    androidTestImplementation libs.androidx.test.uiautomator
    androidTestImplementation libs.androidx.espresso.core
    androidTestImplementation libs.androidx.work.testing
    androidTestImplementation libs.testing.mockwebserver
}

ext {
    gleanNamespace = "mozilla.telemetry.glean"
    gleanPythonEnvDir = gradle.mozconfig.substs.GRADLE_GLEAN_PARSER_VENV
}
apply plugin: "org.mozilla.telemetry.glean-gradle-plugin"
