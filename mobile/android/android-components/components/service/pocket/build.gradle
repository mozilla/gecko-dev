/* This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at http://mozilla.org/MPL/2.0/. */

buildscript {
    repositories {
        gradle.mozconfig.substs.GRADLE_MAVEN_REPOSITORIES.each { repository ->
            maven {
                url = repository
                if (gradle.mozconfig.substs.ALLOW_INSECURE_GRADLE_REPOSITORIES) {
                    allowInsecureProtocol = true
                }
            }
        }
    }

    dependencies {
        classpath  libs.plugin.serialization
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.google.devtools.ksp'
apply plugin: 'kotlin-android'
apply plugin: 'kotlinx-serialization'

android {
    defaultConfig {
        minSdkVersion = config.minSdkVersion
        compileSdk = config.compileSdkVersion
        targetSdkVersion = config.targetSdkVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ksp {
            arg("room.schemaLocation", "$projectDir/schemas".toString())
            arg("room.generateKotlin", "true")
        }
    }

    buildTypes {
        release {
            minifyEnabled = false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        test.assets.srcDirs += files("$projectDir/schemas".toString())
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    buildFeatures {
        buildConfig = true
    }

    namespace = 'mozilla.components.service.pocket'
}

dependencies {
    implementation libs.kotlin.coroutines
    implementation libs.kotlin.json

    implementation libs.androidx.core.ktx
    implementation libs.androidx.work.runtime
    implementation libs.androidx.room.runtime
    ksp libs.androidx.room.compiler

    implementation project(':support-ktx')
    implementation project(':support-base')
    implementation project(':concept-fetch')

    testImplementation libs.kotlin.reflect

    testImplementation libs.androidx.arch.core.testing
    testImplementation libs.androidx.concurrent
    testImplementation libs.androidx.test.core
    testImplementation libs.androidx.test.junit
    testImplementation libs.testing.coroutines
    testImplementation libs.testing.robolectric
    testImplementation libs.androidx.room.testing
    testImplementation libs.androidx.work.testing

    testImplementation project(':support-test')
    testImplementation project(':lib-fetch-httpurlconnection')

    androidTestImplementation project(':support-android-test')

    androidTestImplementation libs.androidx.room.testing
    androidTestImplementation libs.androidx.arch.core.testing
    androidTestImplementation libs.androidx.test.core
    androidTestImplementation libs.androidx.test.runner
    androidTestImplementation libs.androidx.test.rules
}

apply from: '../../../android-lint.gradle'
apply from: '../../../publish.gradle'
ext.configurePublish(config.componentsGroupId, project.name, project.ext.description)
