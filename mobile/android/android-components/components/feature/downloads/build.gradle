/* This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at http://mozilla.org/MPL/2.0/. */

import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'

apply plugin: 'com.google.devtools.ksp'

android {
    defaultConfig {
        minSdkVersion = config.minSdkVersion
        compileSdk = config.compileSdkVersion
        targetSdkVersion = config.targetSdkVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ksp {
            arg("room.schemaLocation", "$projectDir/schemas".toString())
            arg("room.generateKotlin", "true")
        }
    }

    buildTypes {
        release {
            minifyEnabled = false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    buildFeatures {
        viewBinding = true
    }

    testFixtures {
        enable = true
    }

    namespace = 'mozilla.components.feature.downloads'
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions.freeCompilerArgs += "-opt-in=kotlinx.coroutines.ExperimentalCoroutinesApi"
}

dependencies {
    implementation project(':components:browser-state')
    implementation project(':components:concept-fetch')
    implementation project(':components:support-ktx')
    implementation project(':components:support-base')
    implementation project(':components:support-utils')
    implementation project(':components:ui-icons')
    implementation project(':components:ui-widgets')

    implementation libs.androidx.constraintlayout
    implementation libs.androidx.core.ktx
    implementation libs.androidx.lifecycle.livedata
    implementation libs.androidx.localbroadcastmanager
    implementation libs.androidx.paging
    implementation libs.androidx.recyclerview

    implementation libs.kotlin.coroutines

    implementation libs.androidx.room.runtime
    ksp libs.androidx.room.compiler

    testImplementation libs.androidx.test.core
    testImplementation libs.androidx.test.junit
    testImplementation libs.testing.coroutines
    testImplementation libs.testing.robolectric
    testImplementation project(':components:concept-engine')
    testImplementation project(':components:support-test')
    testImplementation project(':components:support-test-libstate')

    androidTestImplementation project(':components:support-android-test')

    androidTestImplementation libs.androidx.room.testing
    androidTestImplementation libs.androidx.arch.core.testing
    androidTestImplementation libs.androidx.test.core
    androidTestImplementation libs.androidx.test.runner
    androidTestImplementation libs.androidx.test.rules
    androidTestImplementation libs.testing.coroutines

    testFixturesImplementation libs.kotlin.coroutines.core

}

apply from: '../../../android-lint.gradle'
apply from: '../../../publish.gradle'
ext.configurePublish(config.componentsGroupId, project.name, project.ext.description)
