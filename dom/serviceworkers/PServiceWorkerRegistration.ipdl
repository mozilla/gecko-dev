/* This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at http://mozilla.org/MPL/2.0/. */

include protocol PBackground;

include ClientIPCTypes;
include DOMTypes;
include IPCNavigationPreloadState;
include IPCServiceWorkerRegistrationDescriptor;

include "ipc/ErrorIPCUtils.h";

namespace mozilla {
namespace dom {

[ChildImpl=virtual, ParentImpl=virtual]
protocol PServiceWorkerRegistration
{
  manager PBackground;

parent:
  async Unregister() returns (bool aSuccess, CopyableErrorResult aRv);
  async Update(nsCString aNewestWorkerScriptUrl) returns (
    IPCServiceWorkerRegistrationDescriptorOrCopyableErrorResult aResult);
  async GetNotifications(nsString aTag) returns (IPCNotificationsOrError aResult);

  // For NavigationPreload interface
  async SetNavigationPreloadEnabled(bool aEnabled) returns (bool aSuccess);
  async SetNavigationPreloadHeader(nsCString aHeader) returns (bool aSuccess);
  async GetNavigationPreloadState() returns (IPCNavigationPreloadState? aState);

child:
  async UpdateState(IPCServiceWorkerRegistrationDescriptor aDescriptor);
  async FireUpdateFound();

both:
  async __delete__();
};

} // namespace dom
} // namespace mozilla
