/* This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this file,
 * You can obtain one at http://mozilla.org/MPL/2.0/. */

/*
 * IPC Transaction protocol for the FedCM DOM API.
 */

include protocol PWindowGlobal;

include "mozilla/dom/IdentityCredentialSerializationHelpers.h";

using mozilla::dom::CredentialMediationRequirement from "mozilla/dom/CredentialManagementBinding.h";
using mozilla::dom::IdentityCredentialDisconnectOptions from "mozilla/dom/IdentityCredentialBinding.h";
using mozilla::dom::IdentityCredentialRequestOptions from "mozilla/dom/IdentityCredentialBinding.h";
using mozilla::dom::IdentityProviderConfig from "mozilla/dom/IdentityCredentialBinding.h";
using mozilla::dom::LoginStatus from "mozilla/dom/LoginStatusBinding.h";


namespace mozilla {
namespace dom {

struct IPCIdentityCredential
{
  nsString id;
  nsString? token;
};


union WebIdentityGetCredentialResponse {
  nsresult;
  IPCIdentityCredential;
};

async protocol PWebIdentity {
  manager PWindowGlobal;

  parent:
    async RequestCancel();

  async GetIdentityCredential(IdentityCredentialRequestOptions aOptions,
                              CredentialMediationRequirement aMediationRequirement,
                              bool aHasUserActivation)
    returns (WebIdentityGetCredentialResponse response);

  async DisconnectIdentityCredential(IdentityCredentialDisconnectOptions aOptions) returns (nsresult rv);

  async PreventSilentAccess() returns (nsresult rv);

  async SetLoginStatus(LoginStatus foo) returns (nsresult rv);

  child:
    async __delete__();
};

}
}
